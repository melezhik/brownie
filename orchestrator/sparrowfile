#!raku

use Sparky::JobApi;

use Ecosystem;

class Pipeline

does Sparky::JobApi::Role

{

  method base-dir {
    "{%*ENV<HOME>}/.brownie"
  }

  method stage-main {

    #bash "rm -rf {self.base-dir}/agents/*", %(
      #description => "remove old registered agents"
    #);

    my @d;

    my $eco = Ecosystem.new;

    say "Testing top 1000 modules on the Raku Ecosystem River:";
    
    for $eco.river.sort(-*.value.elems).map(*.key).head(1000) -> $d {
        push @d, $d;
        say $d;
    }

    "list.txt".IO.spurt(@d.join("\n"));

    my $me = Sparky::JobApi.new(:mine);

    $me.put-file("list.txt","list.txt");

    my @eff-list = @d;
    
    my $j = 0;

    my %seen;

    my $scnt = 0;

    while (True) {

      my @_list = [];
      for @eff-list -> $l {
        next if %seen{$l};
        if "{self.base-dir}/versions/2025.10/{$l}.OK".IO ~~ :f  ||
           "{self.base-dir}/versions/2025.10/{$l}.FAIL".IO ~~ :f  {
              next;
        }
        push @_list, $l;
      }

      @eff-list = @_list;

      #@eff-list.=pick(@eff-list);

      my $cnt = 0;
      
      for @d -> $l {
        if "{self.base-dir}/versions/2025.10/{$l}.OK".IO ~~ :f  ||
           "{self.base-dir}/versions/2025.10/{$l}.FAIL".IO ~~ :f  {
            $cnt++;
        }
      }

      if $cnt == @d.elems {
        last;
      } else {
        say "$cnt tests out of {@d.elems} finished"
      }

      my $chunk = 0;

      my @agents;
      
      my $k = 1;

      my %seen2;

      while (True) {
        say "look for available agents ...";
        if "{self.base-dir}/agents".IO ~~ :d { 
          my @aa = dir("{self.base-dir}/agents").map({$_.basename});
          @aa .= pick(@aa); # sort randomly
          for @aa -> $aa {
              next if %seen2{$aa};
              push @agents, $aa;
              %seen2{$aa} = True;
              say ">>> pick agent $aa";
          }
        }
        say "... sleep for 5 seconds";
        sleep(5);
        $k++;
        last if $k > 4;
        last if @agents.elems >= 10;
      }

      say "{@agents.elems} online agents found";

      if @agents.elems > 0 && @eff-list.elems > 0 {
        if @eff-list.elems < @agents.elems {
            $chunk = 1;
            @agents = @agents[0 .. @eff-list.elems -1];
        } else {
          $chunk = Int(@eff-list.elems/@agents.elems);
          $chunk = 5 if $chunk > 5;
        }
      
        my $a = 0;

        say "agents cnt: {@agents.elems}";

        say "chunk: $chunk";

        for 1 .. @agents.elems -> $i {

          my @slice = @eff-list[$a .. $a + $chunk - 1];

          @slice = @slice.sort;

          say "push job to agent {@agents[$i-1]}";

          my $agents-queue = Sparky::JobApi.new(
            :project<browny.queue>,
            :job-id(@agents[$i-1]),
          );

          $agents-queue.put-stash(
            %( 
              version => "2025.10",
              agent => @agents[$i-1],
              modules => @slice,
              run-id => time, 
            )
          );
          say "slice: {@slice.raku}";
          $a = $a + $chunk;
          say "...";
          
          for @slice -> $s {
            %seen{$s} = True;
            $scnt++;
          }
        }
      }

      say "///";
      say "[{$scnt}] tests out of [{@d.elems}] sent for execution";
      say "///";
    
      say "<<< sleep for 30 seconds";

      sleep(30);

      $j++;
      
      last if $j > 10000;
      
    }

    say "done";

    say "summary";
    my $summary = "";

    for @d -> $m {

      my $status = "?";

      $status = "OK" if "{self.base-dir}/versions/2025.10/{$m}.OK".IO ~~ :f;
      $status = "FAIL" if "{self.base-dir}/versions/2025.10/{$m}.FAIL".IO ~~ :f;

      say "$m ... \t $status";

      $summary ~= "$m ... \t $status\n";
     
    }

    "summary.txt".IO.spurt($summary);
    

    for @d -> $m {
      if "{self.base-dir}/versions/2025.10/{$m}.log".IO ~~ :f {
        $me.put-file("{self.base-dir}/versions/2025.10/{$m}.log","$m.log");
      }
    }

    $me.put-file("summary.txt","summary.txt");

  }
}  

Pipeline.new.run;
