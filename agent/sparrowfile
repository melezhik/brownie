#!raku

use Sparky::JobApi;

use Random::Names;

use JSON::Fast;

class Pipeline

does Sparky::JobApi::Role

{

  method base-dir {
    "{%*ENV<HOME>}/.brownie"
  }
  
  method stage-main {

    my $max-threads = Int(%*ENV<BRW_AGENT_MAX_THREADS> || 3);

    my $random-int = (0..9).flat.roll(8).join("");

    directory self.base-dir unless self.base-dir.IO ~~ :d;

    directory "{self.base-dir}/rakudos" unless "{self.base-dir}/rakudos".IO ~~ :d;

    my $agent-id = "{self.base-dir}/agent-id".IO ~~ :f 
    ?? "{self.base-dir}/agent-id".IO.slurp 
    !! $random-int;

    unless  "{self.base-dir}/agent-id".IO ~~ :f {
      "{self.base-dir}/agent-id".IO.spurt($random-int);
    }

    my $agent = ( %*ENV<BRW_AGENT_NAME_PREFIX> || Random::Names.new.docker-name ) ~ "-" ~ $agent-id;

    say "agent: $agent, max threads: $max-threads";

    file-delete "tasks.tar";

    say "archiving tasks/ to tasks.tar";

    bash "tar cf {$*CWD}/tasks.tar -C tasks/ .";    

    my $orch-job = Sparky::JobApi.new(
      :api(config()<orchestrator-url>),
      :project<browny.queue>,
      :job-id($agent),
    );

    my $orch-job2 = Sparky::JobApi.new(
      :api(config()<orchestrator-url>),
      :project("{$agent}.ping"),
    );


    my $i = 1;

    my $jobs-run-cnt = 0;
    
    while True {

      if ($i == 1) {

        # register agent / send agent heartbeat 
        # every 90 seconds

        task-run "tasks/jobs-cnt";
        $orch-job2.queue({
          description => "heartbeat from $agent",
          tags => %(
            agent => $agent,
            stage => "ping",
            jobs-run-cnt => $jobs-run-cnt,
            max-threads => $max-threads,
            agent-version => %*ENV<BRW_AGENT_VERSION> || "0.0.0",
          ),
        });
        # send local queue and running jobs
        # stat to o10r
        $orch-job2.put-file("q.data","q.data.txt");
        $orch-job2.put-file("r.data","r.data.txt");
        $i++;
      } elsif ($i == 9) {
         $i = 1;
      } else {
         $i++;
      }

      # update jobs counter
      # by taking into account
      # all active jobs
      # and all jobs in queue

      task-run "tasks/jobs-cnt";
      my $r-cnt = Int("r.cnt".IO.slurp.chomp);
      my $q-cnt = Int("q.cnt".IO.slurp.chomp);
      $jobs-run-cnt = $r-cnt + $q-cnt;

      my $q = $orch-job.get-stash();

      say "load requests from orchestrator queue";

      say "jobs-run-cnt: $jobs-run-cnt, max-threads: $max-threads";

      if $q {
        my $rakudo-version = $q<version>;
        if "{self.base-dir}/jobs/{$q<run-id>}".IO ~~ :d {
            say "skip job: {$q<run-id>}, already in agent queue";
        } elsif ($jobs-run-cnt >= $max-threads) {
          say "skip job: agent is busy"
        } else {
          if "{self.base-dir}/rakudos/$rakudo-version".IO ~~ :f {
            say "rakudo version $rakudo-version is already installed, skip installation";
          } else {
            if $q<source-code> {
               task-run "tasks/install-rakudo-from-source", %(
                  version => $rakudo-version,
               ); 
            } else {
              module-run 'Rakudo::Install', %(
                :user<root>,
                :$rakudo-version,
                :!patch-profile,
              );
            }
            directory "{self.base-dir}/rakudos/";
            "{self.base-dir}/rakudos/$rakudo-version".IO.spurt("");
          }

          say "///";
          say "run tests for job: {$q<run-id>}";
          say "///";
          my $j = Sparky::JobApi.new(
            project => "agent.job_{time}",
          );
          $j.put-stash(%(list => $q<modules><>));
          $j.put-file("tasks.tar","tasks.tar");
          my %tags = %(
              rakudo-version => $rakudo-version,
              stage => "test",
              agent => $agent,
              zef_test_degree => %*ENV<ZEF_FETCH_DEGREE>,
              zef_fetch_degree => %*ENV<ZEF_TEST_DEGREE>,
              run-id => $q<run-id>,
          );
          %tags<zef_install_to> = True if %*ENV<ZEF_INSTALL_TO>;
          $j.queue({
            description => "job: {$q<run-id>}", 
            tags => %tags,
          });
          directory "{self.base-dir}/jobs/{$q<run-id>}";
        }
      }
      sleep(5);
      say "<<< sleep for 5 seconds";
    }
  }

  method stage-test {

    my $agent = tags()<agent>;
    
    my $my = Sparky::JobApi.new( :mine );

    my $rakudo-version = tags()<rakudo-version>;

    bash "export PATH=/tmp/whateverable/rakudo-moar/{$rakudo-version}/bin/:/tmp/whateverable/rakudo-moar/{$rakudo-version}/share/perl6/site/bin:\$PATH && which zef > raku.env && zef --version >> raku.env && which raku >> raku.env && raku -V >> rakuenv.txt";

    $my.put-file("rakuenv.txt","rakuenv.txt");

    my $blob = $my.get-file("tasks.tar");

    directory "tasks";

    my $fh = open "tasks/tasks.tar", :w, :bin;
    $fh.write($blob);
    $fh.close;

    bash "tar -xf tasks.tar  && ls -l", %(
      cwd => "{$*CWD}/tasks",
      description => "unpack tasks.tar",
    );

    my @list = $my.get-stash()<list><>;

    say ">>>";

    say "list: \n", @list.join("\n");

    say "<<<";

    my %results;

    my $orch-job = Sparky::JobApi.new(
      :api(config()<orchestrator-url>),
      :project("{$agent}.job_{tags()<run-id>}.report"),
    );

    "list.txt".IO.spurt(@list.join("\n"));

    $my.put-file("list.txt","list.txt");

    my $status = True;

    for @list -> $m {
      my $start_time = time;
      %results{$m} = %( status => "", log => "" );
      my $s = task-run "tasks/run-test", %(
        module => $m,
        zef_install_to => tags()<zef_install_to>,
        zef_test_degree => tags()<zef_test_degree>,
        zef_fetch_degree => tags()<zef_fetch_degree>,
        rakudo_version => $rakudo-version,
      );
      my $els = Int(time - $start_time);
      %results{$m}<time> = $els;
      if $s<success> == 1 {
        %results{$m}<status> = True
      } else {
        %results{$m}<status> = False;
        $status = False;
      }
      if "log.txt".IO ~~ :e {
        %results{$m}<log> = "log.txt".IO.slurp;
        $my.put-file("log.txt","{$m}.log.txt");
      }
    }

    $orch-job.put-stash(%results);
    $orch-job.put-file("rakuenv.txt","rakuenv.txt");

    $orch-job.queue({
      description => "test report from $agent", 
      tags => %(
        version => tags()<rakudo-version>,
        stage => "results",
      ),
    });

    say "results:";
    say "<<<";
    for %results.kv -> $k, $v {
        say "$k ... {$v<status>}\t "
    }  
    say ">>>";

    warn "some tests failed" unless $status;

  }

  method stage-ping {

    directory "{self.base-dir}/agents";
    directory "{self.base-dir}/agents-meta";
    
    "{self.base-dir}/agents/{tags()<agent>}".IO.unlink;

    "{self.base-dir}/agents/{tags()<agent>}".IO.spurt("");

    my $jobs-run-cnt = tags()<jobs-run-cnt> || 0;
    my $max-threads = tags()<max-threads> || 1;
    my $agent-version = tags()<agent-version> || "0.0.0";

    say "ping from agent: {tags()<agent>}, version: {$agent-version}, jobs-run-cnt: {$jobs-run-cnt}, max-threads: $max-threads";

    "{self.base-dir}/agents-meta/{tags()<agent>}.json".IO.spurt(
      to-json(
        %( 
          jobs-run-cnt => $jobs-run-cnt,
          max-threads => $max-threads,
          agent-version => $agent-version,
        ),
      )
    );

  }

  method stage-results {

    my $me = Sparky::JobApi.new( :mine );

    my $resutls = $me.get-stash();
    my $version = tags()<version>;
    my @list;
    my $status = True;
    for $resutls.kv -> $k, $v {
      if "{self.base-dir}/versions/$version/$k".IO ~~ :d {
         say "module $k is already processed";
         next;
      }
      directory "{self.base-dir}/versions/$version/$k";
      my $time = $v<time> || "?";
      if $v<status> {
        "{self.base-dir}/versions/$version/{$k}.OK".IO.spurt("");
        push @list, "$k ... \t OK \t {$time} sec";
        if $v<log> {
          say "log:";
          say ">>>";
          say $v<log>;
        }
      } else {
        "{self.base-dir}/versions/$version/{$k}.FAIL".IO.spurt("");
        push @list, "$k ... \t FAIL \t {$time} sec";
        $status = False;
        if $v<log> {
          say "log:";
          say ">>>";
          say $v<log>;
          "{self.base-dir}/versions/$version/{$k}.log".IO.spurt($v<log>);
          $me.put-file("{self.base-dir}/versions/$version/{$k}.log","{$k}.log");
        }
      }
      "{self.base-dir}/versions/$version/$k/meta.json".IO.spurt(
         to-json($v)
      );

    }

    "summary.txt".IO.spurt(@list.join("\n"));
    $me.put-file("summary.txt","summary.txt");

    say ">>>";
    warn "some tests failed" unless $status;

  }
}


Pipeline.new.run;
